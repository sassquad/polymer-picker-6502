*FX3
IFPAGE<>&E00:PROCr
REMONERRORMODE7:REPORT:PRINT" at line ";ERL:END
N%=RND(-TIME):PROCinit
MODE2:VDU23;8202;0;0;0;:HIMEM=&2BFF
REPEAT
REPEAT
PROCoff
PROCscreen
PROCstart
PROCboat
PROCon
IFlev%>8:PROCfish:PROCshark:ELSEIF(lev%MOD2)=1:PROCfish ELSE PROCshark
PROCbags
PROCstatus
PROCscore
PROCairbar
TIME=0
REPEAT
PROCdiver
PROCair
IFlev%>8:PROCmovefish:PROCmoveshark:ELSEIF(lev%MOD2)=1:PROCmovefish ELSE PROCmoveshark
PROCmovesealife
UNTIL P%=0 OR L%=0 OR O%<928
IFP%=0 AND O%>927 PROCgreat:lev%=lev%+1
UNTIL L%=0 OR O%<928
PROCover
UNTIL0
DEFPROCinit
LOCALa%:FX%=&AB8:FY%=&AC0:FH%=&AC8:FV%=&AD0:F%=&AD8
BX%=&AE0:BY%=&AE8:B%=&AF0:lev%=8:DIMs%(3)
FORa%=0TO3:READs%(a%):NEXT
DATA134,133,143,131
ENDPROC
DEFPROCoff
VDU4,12:FORZ=0TO15:VDU19,Z,0;0;:NEXT:VDU19,14,7;0;17,14,31,5,15
IFlev%<5PRINT"GET READY!":ELSEPRINT"OH BOY...!"
ENDPROC
DEFPROCon
VDU4,17,128,31,5,15:PRINTSPC(10)
FORZ=0TO15:VDU19,Z,Z;0;:NEXT
VDU19,0,4;0;19,15,0;0;19,14,6;0;5
IF(lev%MOD4)=1:VDU19,8,5;0;19,9,3;0;
IF(lev%MOD4)=2:VDU19,8,5;0;19,9,3;0;
IF(lev%MOD4)=3:VDU19,8,6;0;19,9,7;0;
IF(lev%MOD4)=0:VDU19,8,6;0;19,9,7;0;
ENDPROC
DEFPROCscreen
VDU28,0,4,19,0,17,s%((lev%-1)MOD4),12,26,5
IF(lev%MOD4)=1:PROCcirc(40,980,40,3):PROCmt(864,200,64,2):PROCmt(980,200,64,2)
IF(lev%MOD4)=2:PROCcirc(40,860,40,1):PROCmt(864,180,32,15):PROCmt(980,200,64,15)
IF(lev%MOD4)=3:PROCcirc(100,920,40,7):PROCcirc(120,920,40,15):PROCmt(864,180,32,7):PROCmt(980,200,64,7)
IF(lev%MOD4)=0:PROCcirc(40,860,40,5):PROCmt(864,120,32,1):PROCmt(980,200,64,1)
VDU4,28,0,31,19,27,17,143,12,26,28,0,28,19,27,17,132,12,26,5:FORT%=242TO247:VDU23,T%:FORI%=1TO8:VDURND(255):NEXT,
FORy%=127TO160STEP32:FORx%=0TO1279STEP64:VDU18,0,3,25,4,x%;y%;RND(6)+241:NEXT,
FORG=0TO9:h=RND(2)+224:VDU18,0,15,25,4,(G*136)+8;224;h,10,8,224,18,0,2,25,4,G*136;224;h,10,8,224:NEXT
FORG=3TO6:VDU18,0,5,25,4,64+(G*136);192;227:NEXT
ENDPROC
DEFPROCstart
DX%=32:DY%=214:DZ%=1:PROCdraw(DZ%,DX%,DY%)
BW%=7:BH%=7:V%=2:H%=1:P%=8:L%=8:O%=1224:OD%=300:SF%=1:DF%=1:OXY%=0:OXB%=0
IF(lev%MOD2)=1cr%=228:co%=1:ELSE:cr%=229:co%=5
crx%=FNgrx:VDU18,3,co%,25,4,crx%;192;cr%
ENDPROC
DEFPROCboat
VDU18,0,15,25,4,512;920;230,231,232,10,8,8,8,233,234,235,236,18,0,7,25,4,512;924;230,231,232,10,8,8,8,233,234,235,236
ENDPROC
DEFPROCfish
FORA=0TO7:FX%?A=FNrx(1):FY%?A=FNry:FH%?A=3:F%?A=3
IFA>3:FH%?A=1:F%?A=2
FV%?A=8:PROCdraw(F%?A,FX%?A,FY%?A):NEXT
E%=0:ENDPROC
DEFPROCshark
SD%=4:SX%=FNrx(4):SY%=60+(lev%*10)
IFSY%>140:SY%=140
IFSX%<=(DX%+12):SD%=5
PROCdraw(SD%,SX%,SY%)
ENDPROC
DEFPROCmovefish
IFFH%?E%=2ANDFY%?E%=0:E%=(E%+1)MOD8:ENDPROC
PROCdraw(F%?E%,FX%?E%,FY%?E%):FX%?E%=FX%?E%+FH%?E%-2:FY%?E%=FY%?E%+FV%?E%-6
IFFX%?E%<4:FH%?E%=3:F%?E%=3
IFFX%?E%>75:FH%?E%=1:F%?E%=2
IFFY%?E%<64ANDFH%?E%<>2 THEN FV%?E%=8 ELSE IF FY%?E%<48 AND FH%?E%=2 THEN FY%?E%=48
IFFY%?E%>200ANDFH%?E%<>2:FV%?E%=4
PROCdraw(F%?E%,FX%?E%,FY%?E%)
IFFH%?E%<>0PROCfishcheck((FX%?E%+3*(F%?E%-2))*2,FY%?E%-5,E%)
IFFH%?E%<>0:IFlev%>4:IFFNbite(FX%?E%,FY%?E%):PROChurt
E%=(E%+1)MOD8:ENDPROC
DEFPROCmoveshark
OSX%=SX%:OSY%=SY%:OSD%=SD%
IFSX%<=(DX%+12)ANDSX%<62:SX%=SX%+SF%:SD%=5
IFSX%>DX%+12ANDSX%>1:SX%=SX%-SF%:SD%=4
IFSY%<=DY%ANDSY%<194:SY%=SY%+SF%
IFSY%>DY%ANDSY%>64:SY%=SY%-SF%
PROCdraw(OSD%,OSX%,OSY%):PROCdraw(SD%,SX%,SY%)
REMIFFNbite(SX%,SY%):PROChurt
ENDPROC
DEFPROCbags:VDU23,251
IF(lev%MOD4)=1:VDU8,24,56,126,255,62,28,8
IF(lev%MOD4)=2:VDU120,-4,-1,-1,-1,-4,120,0
IF(lev%MOD4)=3:VDU102,153,153,126,126,153,153,102
IF(lev%MOD4)=0:VDU66,66,126,126,126,126,126,126
FORA=0TO7:BX%?A=(A+1)*16:BY%?A=((RND(153)-1)+48):VDU18,3,7,25,4,BX%?A*8;BY%?A*4;251:NEXT
ENDPROC
DEFPROCstatus
VDU26,4,17,143,17,7,31,1,30,251,32,17,7:PRINT;P%
VDU31,6,30,17,6,241,32,17,7:PRINT;L%:VDU31,11,30:PRINT"Air":VDU17,128,5
ENDPROC
DEFPROCscore
VDU4,17,7,31,6,0:PRINT"$"STRING$(6-LENSTR$S%,"0");S%;:VDU5:ENDPROC
DEFPROCairbar
VDU5,18,0,14:MOVE920,40:MOVE1216,40:PLOT85,920,52:PLOT85,1216,52:ENDPROC
DEFPROCair
IFTIME<OD%ENDPROC
O%=O%-(8*DF%):SOUND0,4,6,3
IFO%>912:VDU18,0,15,25,4,O%;52;25,5,O%;40;25,4,O%+8;52;25,5,O%+8;40;
IFO%<1000:VDU19,14,9;0; ELSE VDU19,14,6;0;
IFO%<1068:IFOXY%=0ANDOXB%=0:VDU18,3,6,25,4,612;850;237;:OXB%=1
TIME=0:ENDPROC
DEFPROCdiver
A%=DX%:B%=DY%:C%=DZ%
IFINKEY-67ANDDX%<67:DX%=DX%+DF%:DZ%=1
IFINKEY-98ANDDX%>1:DX%=DX%-DF%:DZ%=0
IFINKEY-73ANDDY%<212:DY%=DY%+(DF%*2)
IFINKEY-105ANDDY%>54:DY%=DY%-(DF%*2)
IFINKEY-74AND(DX%<>A% OR DY%<>B%):DF%=2:OD%=100:PROCair:ELSE:DF%=1:OD%=200
PROCdraw(C%,A%,B%):PROCdraw(DZ%,DX%,DY%):CX%=DX%+6:CY%=DY%-14:PROCcheck(CX%*2,CY%)
REMIFFNrs(CX%,CY%)=12 ORFNrs(CX%+1,CY%)=12 ORFNrs(CX%+2,CY%)=12 ORFNrs(CX%+3,CY%)=12 OD%=150:SF%=2:PROChurt ELSEOD%=300
IFDX%>30 ANDDX%<50 ANDDY%>211 ANDO%<1068 AND OXY%=0:PROCgotair
IFINKEY-56VDU4,17,143,17,7,31,7,29:PRINT"Paused":REPEATUNTILINKEY-54:VDU17,143,31,7,29:PRINTSPC6
IFINKEY-17VDU4,17,143,17,7,31,5,31:PRINT;"Quiet Mode";:*FX210,1
IFINKEY-82VDU4,17,143,17,7,31,5,31:PRINT;SPC10;:*FX210,0
VDU5:ENDPROC
DEPROCp(S$):VDU4,1
DEFPROCcheck(X%,Y%)
C%=FNrc(X%,Y%)
IFC%<8:P%=P%-1:S%=S%+200:SOUND1,1,99,1:VDU18,3,7,25,4,BX%?C%*8;BY%?C%*4;251,4:BX%?C%=255:BY%?C%=255:PRINTTAB(13-LENSTR$S%,0);S%
PROCstatus
ENDPROC
DEFFNbite(i%,j%)
IFi%<DX% OR j%>DY% OR i%>(DX%+12) OR j%<(DY%-16):=FALSE:ELSE:=TRUE
DEFPROCfishcheck(X%,Y%,E%)
C%=FNrc(X%,Y%)
IFC%<8:PROCdeadfish(E%,C%)
ENDPROC
DEFPROCdeadfish(E%,C%)
P%=P%-1:L%=L%-1:SOUND0,-10,50,10
PROCdraw(F%?E%,FX%?E%,FY%?E%):F%?E%=F%?E%+4:PROCdraw(F%?E%,FX%?E%,FY%?E%)
FH%?E%=2:FV%?E%=0:PROCstatus
VDU18,3,7,25,4,BX%?C%*8;BY%?C%*4;251:BX%?C%=255:BY%?C%=255:ENDPROC
DEFPROCmovesealife
ocx%=crx%:crx%=crx%+6:IFcrx%>1260crx%=0
IFcrx%<>ocx%:VDU5,18,3,co%,25,4,ocx%;192;cr%,25,4,crx%;192;cr%
ENDPROC
DEFPROCgotair
O%=1224:SOUND&12,2,150,8:PROCairbar:OXY%=1:OXB%=0:SF%=1:VDU18,3,6,25,4,612;850;237;
ENDPROC
DEFPROChurt
OD%=150:PROCair:SF%=2:SOUND1,-5,255,1:VDU18,3,1
FORz=1TO4:PLOT69,(DX%*16)+RND(64),(DY%*4)+RND(64):NEXT
ENDPROC
DEFPROCgreat
IFlev%>8:PROCbon:ELSE:IF(lev%MOD2)=1:PROCbon
VDU4,31,5,2:PRINT"NICE ONE!":TIME=0:REPEATUNTILTIME>300
TIME=0:VDU5:ENDPROC
DEFPROCbon
FORA=0TO7
IFFH%?A<>2ANDFY%?A<>0:SOUND2,-10,(30+A*10),1:S%=S%+50:PROCscore:VDU18,0,1,25,4,FX%?A*16;(FY%?A*4)+40;238
FORz=1TO500:NEXT
NEXT
ENDPROC
DEFPROCover
VDU4,31,7,2:IFP%<L%PRINT"NICE!" ELSE PRINT"POOR!"
VDU31,4,10:PRINT"Hit SPACE to":VDU31,5,12:PRINT"play again"
REPEATUNTILGET=32:lev%=8
ENDPROC
DEFPROCcirc(h%,v%,r%,c%)
VDU24,0;864;1279;1023;29,h%;v%;18,0,c%:x%=r%:y%=0:z%=x%/2
REPEAT:VDU25,4,x%;y%;25,5,-x%;y%;25,4,y%;x%;25,5,-y%;x%;25,4,x%;-y%;25,5,-x%;-y%;25,4,y%;-x%;25,5,-y%;-x%;
y%=y%+1:z%=z%-y%
IFz%<0:z%=z%+x%:x%=x%-1
UNTILx%<y%
ENDPROC
DEFPROCmt(mx%,LE%,HE%,CO%)
ox%=-32:oy%=0:L=LE%/PI:VDU29,mx%;864;25,4,0;0;
FORx%=0TOLE%STEP64
y%=40+RND(40)+HE%*SIN(x%/L):VDU18,0,CO%,25,4,x%;y%;25,4,x%;0;:PLOT85,ox%,oy%:PLOT85,ox%,0
VDU18,0,15,25,4,ox%;oy%+4;25,5,x%;y%+4;:ox%=x%:oy%=y%:NEXT
VDU25,5,x%+16;4;18,0,CO%,25,4,ox%;0;25,4,ox%;oy%;:PLOT85,x%+16,0
ENDPROC
DEFPROCdraw(A%,X%,Y%):CALLW%:ENDPROC
DEFFNrx(W):W=W*4:=(RND(73-W)-1)+4
DEFFNry:=(RND(153)-1)+48
DEFFNrc(X%,Y%):CALLQ%:=?&AB2
DEFFNrs(X%,Y%):CALLR%:addr%=!&70 AND &FFFF:=?addr%
DEFFNgrx:=(RND(32)+2)*32
DEFFNgry:=(RND(33)+18)*16
DEFPROCr:*T.
*FX138,0,128
*FX3,2
IFPAGE>&E00:*KEY0FORT%=0TOTOP-PAGE STEP4:T%!&E00=T%!PAGE:N.|MPAGE=&E00|MOLD|MRUN|M
